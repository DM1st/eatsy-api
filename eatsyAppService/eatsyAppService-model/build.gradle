dependencies {

    //Java utilities/helper libraries
    implementation "org.apache.commons:commons-lang3:$commonsLang3Version"
    // https://projectlombok.org
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    //Spring swagger spec and ui
    implementation "org.springdoc:springdoc-openapi-ui:$springDocVersion"

    // Test dependencies
    // (In JUnit 5 the API is separated from the runtime)
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    //EqualsVerifier
    testImplementation "nl.jqno.equalsverifier:equalsverifier:$equalsVerifierVersion"
    //ToStringVerifier
    testImplementation "com.jparams:to-string-verifier:$toStringVerifierVersion"

}

//Required so that spring boot builds a layered jar for effective docker image
bootJar {
    layered {
        application {
            intoLayer("spring-boot-loader") {
                include("org/springframework/boot/loader/**")
            }
            intoLayer("application")
        }
        dependencies {
            intoLayer("module-dependencies") {
                include("org.eatsy:*:*")
            }
            intoLayer("dependencies")
        }
        layerOrder = ["dependencies", "spring-boot-loader", "module-dependencies", "application"]
    }
}